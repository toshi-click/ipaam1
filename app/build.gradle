// 必須
apply plugin: 'com.android.application'
//apply plugin: 'com.google.android.gms.oss-licenses-plugin'
//apply plugin: 'io.fabric'
// kotlin関連 kotlin系のapplyは他のapplyの後にやったほうが問題が少ない
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

// バージョン番号を変数にしたほうがバージョン管理した時わかりやすかった
def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

// パッケージ名定数にしておく
def packageName = "click.toshi.ipaam1"

android {
    flavorDimensions "default"
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        applicationId packageName
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        getByName("debug") {
            storeFile = rootProject.file("debug.jks")
            storePassword = "debugdebug"
            keyAlias = "debugdebug"
            keyPassword = "debugdebug"
        }
        create("release") {
            storeFile = rootProject.file("release.jks")
            storePassword = System.getenv("RELEASE_STORE_PASSWORD")
            keyAlias = System.getenv("RELEASE_KEY_ALIAS")
            keyPassword = System.getenv("RELEASE_KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // Lintのオプション指定
    lintOptions {
        // issue IDを設定することで、設定したissue IDを無視
        disable 'InvalidPackage'
        // falseを設定することで、build中にlintのエラーが出てもbuildを止めないようにする
        abortOnError false
        // textReportとtextOutputを設定するとコマンドラインのビルドでlintのwarningやerrorが表示されるので
        // たとえばCIがlint errorで落ちたときに手元でlintを走らせることなく原因がわかる
        textReport true
        textOutput 'stdout'
        // lintの設定ファイルを指定
        lintConfig file("lint.xml")
    }
    testOptions {
        unitTests.all {
            // configure the set of classes for JUnit tests
            include project.hasProperty("testFilter") ? "**/*${project.ext.testFilter}*Test.class" : '**/*Test.class'
            exclude '**/espresso/**/*.class'
            // configure max heap size of the test JVM
            maxHeapSize = '2048m'
            // configure the test JVM arguments
            jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'
            // Specify max number of processes (default is 1)
            maxParallelForks = 1
            // Specify max number of test classes to execute in a test process
            // before restarting the process (default is unlimited)
            forkEvery = 150
            // configure whether failing tests should fail the build
            ignoreFailures false
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardError'
            }
        }
    }
}

dependencies {
    // Support libraries
    implementation deps.support.app_compat
    implementation deps.support.v4
    implementation deps.support.design
    //    implementation "androidx.recyclerview:recyclerview:$support_lib_version"
//    implementation "com.android.support:cardview-v7:$support_lib_version"
//    implementation "com.android.support:customtabs:${support_lib_version}"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
//    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'

    // Architecture components
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    kapt deps.lifecycle.compiler
    implementation deps.room.runtime
    kapt deps.room.compiler
    implementation deps.room.rxjava2

    // RxJava
    implementation deps.rx_android
    implementation deps.rxjava2

    // firebase
    // https://firebase.google.com/docs/android/setup?hl=ja#available_libraries
//    implementation "com.google.firebase:firebase-core:16.0.3"
//    implementation "com.crashlytics.sdk.android:crashlytics:2.9.5"
//    implementation "com.google.firebase:firebase-auth:16.0.3"
//    implementation "com.google.firebase:firebase-database:16.0.2"
//    implementation "com.google.firebase:firebase-firestore:17.1.0"
//    implementation "com.google.firebase:firebase-storage:16.0.2"
//    implementation "com.google.firebase:firebase-messaging:17.3.2"
//    implementation "com.google.firebase:firebase-ads:15.0.1"

    implementation "com.jakewharton.timber:timber:4.7.0"
    // kotlin
    // Kotlin
    implementation deps.kotlin.stdlib
    // kotlin
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // 自作ライブラリは最後に読み込むように
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Dependencies for local unit tests
    testImplementation deps.junit
    testImplementation deps.mockito.all
    testImplementation deps.hamcrest
    testImplementation deps.arch_core.testing
    testImplementation deps.kotlin.stdlib
    testImplementation deps.kotlin.test

    // Android Testing Support Library's runner and rules
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.room.testing
    androidTestImplementation deps.arch_core.testing

    // Dependencies for Android unit tests
    androidTestImplementation deps.junit
    androidTestImplementation deps.mockito.core, { exclude group: 'net.bytebuddy' }
    androidTestImplementation deps.dexmaker

    // Espresso UI Testing
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib
    androidTestImplementation deps.espresso.intents

    // Resolve conflicts between main and test APK:
    androidTestImplementation deps.support.annotations
    androidTestImplementation deps.support.v4
    androidTestImplementation deps.support.app_compat
    androidTestImplementation deps.support.design

}
// MUST BE AT THE BOTTOM
// firebaseでgoogle-services.jsonを生成してから有効にすること
//apply plugin: 'com.google.gms.google-services'
